buildscript {
    repositories {
        jcenter()
        mavenCentral()
        gradlePluginPortal()

        maven {
            url "http://sandec.bintray.com/repo"
        }
    }

    dependencies {
        classpath "SANDEC:simplefx-plugin-gradle:$SIMPLEFX_VERSION"
        classpath "com.sandec.jpro:jpro-plugin-gradle:$JPRO_VERSION"
        classpath 'org.gretty:gretty:2.3.1'
        classpath 'org.openjfx:javafx-plugin:0.0.8'
        classpath 'org.beryx.runtime:org.beryx.runtime.gradle.plugin:1.8.4'
    }
}

def appName = "app-template"

version = APP_VERSION

System.setProperty("file.encoding", "UTF-8")

project.ext {
    env = MYAPP_ENV
}




configure([project(':shared'),project(':client'),project(':server'),project(':mobile'),project(':web')]) {
    apply plugin: 'java'
    apply plugin: 'de.sandec.simplefx'

    version = APP_VERSION

    repositories {
        jcenter()
        maven {
            url "http://sandec.bintray.com/repo"
        }
        maven {
            url 'https://nexus.gluonhq.com/nexus/content/repositories/releases/'
        }
    }

    targetCompatibility = "8"
    sourceCompatibility = "8"

    dependencies {
        testCompile 'junit:junit:4.12'
    }
}

configure([project(':shared')]) {
    apply plugin: 'org.openjfx.javafxplugin'

    javafx {
        version = "$JAVAFX_VERSION"
        configuration = 'compile'
        modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web', 'javafx.swing' ]
    }
}


configure([project(':client')]) {

    apply plugin: 'com.sandec.jpro'
    //apply plugin: 'org.javafxports.jfxmobile'
    apply plugin: 'org.beryx.runtime'


    mainClassName = 'myapp.client.Starter11'

    applicationDefaultJvmArgs = ["-Dconfig.file=../config/$env/client.conf"]

    dependencies {
        compile project(':shared')
        compile "SANDEC:simplefx-remoting-client:$SIMPLEFX_REMOTING_VERSION"
        compile "com.sandec.jpro:jpro-web-core:$JPRO_WEB_VERSION"
        compile "com.sandec.jpro:jpro-crossplatform-desktop:$JPRO_CROSSPLATFORM"
        compile "SANDEC:jnodes:$JNODES_VERSION"
        compile "SANDEC:jnodes-devices:$JNODES_VERSION"
        compile "org.fxmisc.cssfx:cssfx:$CSSFX_VERSION"
        compile "org.scalaj:scalaj-http_$SCALA_VERSION:2.4.2"
    }

    runtime {
        options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
        modules = ['jdk.unsupported']

        jpackage {
            imageOptions = ['--icon', 'src/main/resources/myapp/logo.icns']
            imageName = "app-template-client"

            if(org.gradle.internal.os.OperatingSystem.current().windows) {
                installerType = 'msi'
                // imageOptions = ['--win-console']
                installerOptions = ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            }
        }
    }

}

configure([project(':server')]) {
    apply plugin: 'war'
    apply plugin: 'org.gretty'

    dependencies {
        compile project(':shared')
        compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.15'
        compile "SANDEC:simplefx-remoting-server:$SIMPLEFX_REMOTING_VERSION"
        compile 'com.h2database:h2:1.4.192'
        compile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.2.1'
        compile "com.typesafe.slick:slick-hikaricp_$SCALA_VERSION:$SLICK_VERSION"
        compile group: 'org.apache.commons', name: 'commons-email', version: '1.5'
        compile "org.scalaj:scalaj-http_$SCALA_VERSION:2.4.1"
    }

    gretty {
        servletContainer = "jetty9.4"
        contextPath = "/"
        integrationTestTask = 'client:test'
        jetty9Version = "$JETTY_VERSION"
        jvmArgs = ["-Dconfig.file=../config/$env/server.conf"]
    }
    test {
        jvmArgs = ["-Dconfig.file=../config/$env/server.conf"]
        testLogging.showStandardStreams = true
    }
}

configure([project(':web')]) {

    apply plugin: 'com.sandec.jpro'

    applicationDefaultJvmArgs = ["-Dconfig.file=../config/$env/client.conf"]

    mainClassName = project(':client').mainClassName

    dependencies {
        compile project(':client')

        compile "com.sandec.jpro:jpro-crossplatform-jpro:$JPRO_CROSSPLATFORM"
    }

    jproStartCommand {
        jvmArgs "-Dconfig.file=./config/$env/client.conf"
    }

    jpro {
        port = 8081
        openingPath = "/"
    }
}

configure([project(':mobile')]) {

    repositories {
        jcenter()
    }

    apply plugin: 'java'
    apply plugin: 'application'
    //apply plugin: 'org.javafxports.jfxmobile'
    apply plugin: 'com.sandec.jpro'


    mainClassName = project(':client').mainClassName

    dependencies {
        compile project(':client')

    //    iosRuntime "com.sandec.jpro:jpro-crossplatform-ios:$JPRO_CROSSPLATFORM"
    //    androidRuntime "com.sandec.jpro:jpro-crossplatform-android:$JPRO_CROSSPLATFORM"
    }
    
    // Here on might add more project specific gluon configurations

}

def bintrayRepo = "sandec/private/"
def bintrayKey = "-u$BINTRAY_USER:$BINTRAY_KEY"

task publishDMG(type: Exec, dependsOn: ["client:jpackage"]) {

  def args = ['curl', '-T', "client/build/jpackage/client-${version}.dmg", bintrayKey, "https://api.bintray.com/content/$bintrayRepo$appName/$version/$appName-${version}.dmg"]
  println("args: " + args.toString().replace(","," "))
  commandLine = args
}
task publishMSI(type: Exec, dependsOn: ["client:jpackage"]) {

  def args = ['curl', '-T', "client/build/jpackage/client-${version}.msi", bintrayKey, "https://api.bintray.com/content/$bintrayRepo$appName/$version/$appName-${version}.msi"]
  println("args: " + args.toString().replace(","," "))
  commandLine = args
}
